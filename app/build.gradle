apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "ru.art2000.calculator"
        minSdkVersion 18
        targetSdkVersion 30
        versionCode 2

        def major = 1
        def minor = 1
        def patch = 1

        versionName "$major.$minor.$patch"

        multiDexEnabled true
        buildConfigField "String", "BUILD_DATE", '"' + getBuildDate() +'"'

        setProperty("archivesBaseName", "RTCalculator-$versionName")
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

    buildTypes {
        debug {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-common.pro', 'proguard-rules-debug.pro'
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-common.pro', 'proguard-rules-release.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

static def getBuildDate() {
    def date = new Date()
    return date.dateTimeString
}

dependencies {
    android.defaultConfig.vectorDrawables.useSupportLibrary = true
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation 'androidx.recyclerview:recyclerview:1.2.0-beta01'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.sothree.slidinguppanel:library:3.4.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'

    def room_version = "2.2.6"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'io.reactivex.rxjava3:rxjava:3.0.10'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'


    def multidex_version = "2.0.1"
    implementation "androidx.multidex:multidex:$multidex_version"


    def okhttp_version = "3.12.12"
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttp_version"

    def retrofit_version = "2.6.0"
    //noinspection GradleDependency
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    //noinspection GradleDependency
    implementation ("com.squareup.retrofit2:converter-simplexml:$retrofit_version") {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }

    testImplementation 'junit:junit:4.13.1'
}
repositories {
    mavenCentral()
}
